{
  lib,
  stdenvNoCC,
  fetchurl,
  nixosTests,
  jre,
  mcVersion,
  buildNumber,
  url,
  sha256,
  minecraft-server,
}:
let
  version = "${mcVersion}-build.${buildNumber}";
in
stdenvNoCC.mkDerivation {
  pname = "paper";
  inherit version;

  src = fetchurl { inherit url sha256; };

  preferLocalBuild = true;

  installPhase = ''
    mkdir -p $out/bin $out/lib/minecraft
    cp -v $src $out/lib/minecraft/server.jar

    cat > $out/bin/minecraft-server << EOF
    #!/bin/sh
    exec ${jre}/bin/java \$@ -jar $out/lib/minecraft/server.jar nogui
    EOF

    chmod +x $out/bin/minecraft-server
  '';

  dontUnpack = true;

  passthru = {
    updateScript = ./update.py;
    # If you plan on running paper without internet, be sure to link this jar
    # to `cache/mojang_{version}.jar`.
    vanillaJar = "${minecraft-server}/lib/minecraft/server.jar";
    nix-minecraft = {
      type = "paper";
      inherit mcVersion;
      loaderVersion = version;
    };
  };

  meta = with lib; {
    description = "A high performance spigot fork";
    homepage = "https://papermc.io";
    license = licenses.gpl3Only;
    platforms = platforms.unix;
    maintainers = with maintainers; [ misterio77 ];
    mainProgram = "minecraft-server";
  };
}
